plugins {
    id 'com.android.application'
}

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId "com.naruto.templateproject"
        minSdkVersion 22
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true
    }

    //签名
    signingConfigs {
        config {
            storeFile file(KEY_PATH)
            storePassword KEY_PW
            keyAlias KEY_ALIAS
            keyPassword ALIAS_PW
        }
    }

    buildTypes {
/*        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }*/

        debug {
            minifyEnabled false//代码混淆
            shrinkResources false//资源压缩
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", "LOG_DEBUG", "true"
            buildConfigField "long", "TIMESTAMP", System.currentTimeMillis() + "L"
            signingConfig signingConfigs.config

            // xml布局文件调用
            resValue "string", "version_name", "${defaultConfig.versionName}"
            resValue "string", "version_code", "${defaultConfig.versionCode}"
        }

        release.initWith(debug)
        release {
            minifyEnabled true//代码混淆
            shrinkResources true//资源压缩
            buildConfigField "boolean", "LOG_DEBUG", "false"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            if (output.outputFile != null && output.outputFileName.endsWith('.apk')) {
                output.outputFileName = "TemplateProject_v${android.defaultConfig.versionName}_${new Date().format("yyyyMMdd")}.apk"
            }
        }
    }

}

dependencies {
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'

    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}